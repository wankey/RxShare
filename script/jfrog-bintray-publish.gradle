apply plugin: 'com.jfrog.bintray'

if (gradle.getStartParameter().getTaskNames().contains("bintrayUpload")) {
    println("> Publishing: " + libraryName + " v" + versioning.name)

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    version = versioning.name

    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")
        configurations = ['archives']
        override = true

        pkg {
            repo = bintrayRepo
            name = bintrayName
            desc = libraryDescription
            websiteUrl = siteUrl
            licenses = allLicenses
            vcsUrl = gitUrl
            labels = libraryLabels
            publish = true
            publicDownloadNumbers = true
            version {
                name = versioning.name
                desc = libraryDescription
                released = new Date()
                gpg {
                    sign = true //Determines whether to GPG sign the files. The default is false
                    passphrase = properties.getProperty("bintray.passphrase")
                }
                mavenCentralSync {
                    sync = true //[Default: true] Determines whether to sync the version to Maven Central.
                    user = properties.getProperty("nexus.user")
                    password = properties.getProperty("nexus.password")
                    close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
                }
            }
        }
    }
}