apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'


android {
  compileSdkVersion 28
  Properties properties = new Properties()
  properties.load project.rootProject.file('local.properties').newDataInputStream()
  def APP_ID = properties.getProperty("APP_ID")
  def WB_ID = properties.getProperty("WB_ID")
  def WX_ID = properties.getProperty("WX_ID")
  def QQ_ID = properties.getProperty("QQ_ID")

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    applicationId APP_ID
    buildConfigField("String", "WEIBO_ID", "\"${WB_ID}\"")
    buildConfigField("String", "WX_ID", "\"${WX_ID}\"")
    buildConfigField("String", "QQ_ID", "\"${QQ_ID}\"")
    manifestPlaceholders = ["TENCENT_SCHEME": "tencent${QQ_ID}"]
  }

  signingConfigs {
    sign {
      storeFile file(ANDROID_STORE_FILE_DJ)
      storePassword ANDROID_STORE_PASSWORD_DJ
      keyAlias ANDROID_KEY_ALIAS_DJ
      keyPassword ANDROID_KEY_PASSWORD_DJ
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.sign
    }

    release {
      minifyEnabled false
      signingConfig signingConfigs.sign

      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation 'androidx.appcompat:appcompat:1.0.0-beta01'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.2'
  implementation 'com.google.android.material:material:1.0.0-beta01'
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
  implementation "io.reactivex.rxjava2:rxjava:2.2.0"

  implementation 'com.sina.weibo.sdk:core:4.2.9:openDefaultRelease@aar'
  implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:5.1.4'
  implementation project(":share")
}
